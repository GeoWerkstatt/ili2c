allprojects {
	group 'ch.interlis'
	apply plugin: "java"
	apply plugin: "maven"
	
	sourceCompatibility = "1.6" 
	targetCompatibility = "1.6"
	compileJava.options.encoding = 'US-ASCII'
	
	configurations {
		deployerJars
	}
	dependencies {
		testCompile group: 'junit', name: 'junit', version: '4.12'
		deployerJars "org.apache.maven.wagon:wagon-ftp:3.3.3"
		deployerJars "org.apache.maven.wagon:wagon-ssh:3.3.3"
	}
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "http://jars.interlis.ch"
		}
	}
}

version '5.1.2'+System.getProperty('release','-SNAPSHOT')

configurations {
    ftpAntTask
}

// to get the latest SNAPSHOT uncomment the following lines
//configurations.all {
    // check for updates every build
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

dependencies {
    implementation "ch.interlis:iox-api:1.0.3"
    implementation("ch.interlis:iox-ili:1.21.0") {
    	exclude group: 'net.iharder', module: 'base64'
    	exclude group: 'com.vividsolutions', module: 'jts-core'
    }
    
    ftpAntTask "org.apache.ant:ant-commons-net:1.10.7"
    
    implementation project('ili2c-core')
}


Properties properties = new Properties()
File propFile=project.rootProject.file('user.properties')
if(propFile.exists()){
	properties.load(propFile.newDataInputStream())
}
ext {
	git = System.getProperty('git',properties.get('git','git'))
	repos_pwd = System.getProperty('repos_pwd',properties.get('repos_pwd','repos_pwd'))
	repos_usr = System.getProperty('repos_usr',properties.get('repos_usr','repos_usr'))
	repos_url = System.getProperty('repos_url',properties.get('repos_url','ftp://ftp.interlis.ch'))
}
def downloads_pwd = System.getProperty('downloads_pwd',properties.get('downloads_pwd','downloads_pwd'))
def downloads_usr = System.getProperty('downloads_usr',properties.get('downloads_usr','downloads_usr'))

sourceSets {
    test {
        java {
            srcDirs = ['test/src']
        }
        resources {
            srcDirs = ['test/data']
        }
    }
}

task fatjar(type: Jar) {
	archiveFileName='ili2c.jar'
  manifest {
    attributes(
      "Main-Class": "ch.interlis.ili2c.Main"
    )
  }
  from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}


task bindist(type: Zip, dependsOn: fatjar) {
	baseName = project.name
	destinationDir = file('dist')
	from fatjar
	from("doc"){
		include "LICENSE.*"
		include "CHANGELOG.txt"
		include "README.txt"
		include "index.html" 
		into('doc')
	}
	from("standard") {
		include("**/*.ili")
		into('standard')
	}
	from ("xsd"){
		include "**/*.xsd"
		into "xsd"
	}
	from ("iliuml") {
		include "*.uml"
		into "iliuml"
	}
}

artifacts {
	archives(bindist.archivePath) {
        type 'zip'
        classifier 'bindist'
        builtBy bindist
    }
    archives(jar.archivePath){
    	builtBy jar
    }
}

task printArchives {
	doLast {
		println jar.archivePath
		println project(":ili2c-core").jar.archivePath
	}
}

test {
    testLogging.exceptionFormat = 'full'
}

uploadArchives {
    repositories {
        mavenDeployer {
        	configuration = configurations.deployerJars
			repository(url: repos_url){
            	authentication(userName: repos_usr, password: repos_pwd)
            }
        }
    }
}



task uploadBin(dependsOn: bindist) {
	doLast {
		ant.taskdef(name: 'ftp',
					classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
					classpath: configurations.ftpAntTask.asPath)
		def dist= bindist.archiveFile.get().asFile.parent
		def name=bindist.archiveFile.get().asFile.name
		def json = groovy.json.JsonOutput.toJson([filename: 'https://downloads.interlis.ch/ili2c/'+name, version: project.version ,date: new Date().format( 'yyyy-MM-dd' )])
		def releaseFile = new File(dist,"ili2c-release.json")
		releaseFile.write(json)
		ant.ftp(server: "jql.ftp.infomaniak.com", userid: downloads_usr, password: downloads_pwd, 
			remoteDir: "/ili2c", passive:"yes") {
				fileset(dir: dist ) {
					include(name: name)
					include(name: releaseFile.name)
				}
		}
	}
}